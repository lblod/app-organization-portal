INSERT {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
      ?ocmwBestuursorgaanT <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?gemeenteFunctie.
  }
}
WHERE {

  VALUES ?orgaanClassification {
    <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/62644b9c-4514-41dd-a660-4c35257f2b35>
    <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/39854196-f214-4688-87a1-d6ad12baa2fa>
    <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/11f0af9e-016c-4e0b-983a-d8bc73804abc>
    <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/ed40469e-3b6f-4f38-99ba-18912ee352b0>
  }

  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    # OCMW
    ?ocmwBestuur a <http://data.vlaanderen.be/ns/besluit#Bestuurseenheid>;
      <http://www.w3.org/ns/org#classification> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002>;
      <http://www.w3.org/ns/org#linkedTo> ?gemeenteBestuur.

    ?ocmwBestuursorgaan a <http://data.vlaanderen.be/ns/besluit#Bestuursorgaan>;
      <http://data.vlaanderen.be/ns/besluit#bestuurt> ?ocmwBestuur;
      <http://www.w3.org/ns/org#classification> ?orgaanClassification.

    ?ocmwBestuursorgaanT <https://data.vlaanderen.be/ns/generiek#isTijdspecialisatieVan> ?ocmwBestuursorgaan.

    FILTER NOT EXISTS {
      ?ocmwBestuursorgaanT <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?ocmwFunctie.
    }

    ?gemeenteBestuur a <http://data.vlaanderen.be/ns/besluit#Bestuurseenheid>;
    # GEMEENTE
    <http://www.w3.org/ns/org#classification> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001>.

   ?gemeenteBestuursorgaan a <http://data.vlaanderen.be/ns/besluit#Bestuursorgaan>;
       <http://data.vlaanderen.be/ns/besluit#bestuurt> ?gemeenteBestuur;
       <http://www.w3.org/ns/org#classification> ?orgaanClassification.

   ?gemeenteBestuursorgaanT <https://data.vlaanderen.be/ns/generiek#isTijdspecialisatieVan> ?gemeenteBestuursorgaan;
     <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?gemeenteFunctie.
  }
}