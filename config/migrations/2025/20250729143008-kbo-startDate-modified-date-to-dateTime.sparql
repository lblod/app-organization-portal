DELETE {
  GRAPH ?g {
    ?s ?p ?date .
  }
}
INSERT {
  GRAPH ?g {
    ?s ?p ?dateTime .
  }
}
WHERE {
  VALUES ?g {
    <http://mu.semte.ch/graphs/administrative-unit>
    <http://mu.semte.ch/graphs/worship-service>
  }

  GRAPH ?g {
    ?s a <http://mu.semte.ch/vocabularies/ext/KboOrganisatie> ;
       ?p ?date .

    VALUES ?p {
      <http://purl.org/dc/terms/modified>
      <http://mu.semte.ch/vocabularies/ext/startDate>
    }

    FILTER (datatype(?date) != xsd:dateTime)
    FILTER (regex(str(?date), "^[0-9]{4}-[0-9]{2}-[0-9]{2}"))

    BIND(STRDT(CONCAT(STR(?date), "T02:00:00"), xsd:dateTime) AS ?dateTime)
  }
}
