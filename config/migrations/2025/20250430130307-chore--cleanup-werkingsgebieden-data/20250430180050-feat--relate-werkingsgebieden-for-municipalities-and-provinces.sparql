PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?municipalityLocation geo:sfWithin ?provinceLocation .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/shared> {
    ?municipality a besluit:Bestuurseenheid ;
                  org:classification <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> .
    ?province a besluit:Bestuurseenheid ;
              org:classification <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000000> .
  }

  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?membership a org:Membership ;
                org:member ?municipality ;
                org:organization ?province .

    ?municipality besluit:werkingsgebied ?municipalityLocation .
    ?province besluit:werkingsgebied ?provinceLocation .
  }

}
