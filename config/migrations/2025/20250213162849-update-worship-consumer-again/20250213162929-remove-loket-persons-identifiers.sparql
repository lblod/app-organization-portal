PREFIX ch: <http://data.lblod.info/vocabularies/contacthub/>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>

# Delete identifiers
DELETE {
  GRAPH <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
    ?identifier ?pidentifier ?oidentifier .
    ?structuredIdentifier ?pstructuredIdentifier ?ostructuredIdentifier .
  }
} WHERE {
  VALUES ?g {
    <http://mu.semte.ch/graphs/shared>
    <http://mu.semte.ch/graphs/worship-service>
  }

  GRAPH ?g {
    ?person a person:Person .
  }

  FILTER EXISTS {
    GRAPH <http://mu.semte.ch/graphs/worship-service> {
      ?agent a ch:AgentInPositie ;
        mandaat:isBestuurlijkeAliasVan|org:heldBy ?person .
    }
  }

  GRAPH <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
    ?person persoon:registratie ?identifier .

    ?identifier generiek:gestructureerdeIdentificator ?structuredIdentifier ;
      ?pidentifier ?oidentifier .

    ?structuredIdentifier ?pstructuredIdentifier ?ostructuredIdentifier .
  }
}

;

DELETE {
  GRAPH <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
    ?structuredIdentifier ?pstructuredIdentifier ?ostructuredIdentifier .
  }
} WHERE {

  GRAPH <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
    ?structuredIdentifier a <https://data.vlaanderen.be/ns/generiek#GestructureerdeIdentificator>;
      ?pstructuredIdentifier ?ostructuredIdentifier.
  }

}