PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH ?g {
    ?memberOrganization org:memberOf ?organization .
  }
}
INSERT {
  GRAPH ?g {
    ?membership a org:Membership ;
      mu:uuid ?uuid ;
      org:organization ?organization ;
      org:member ?memberOrganization ;
      org:role <http://data.lblod.info/id/rollen/2152eb830b1143bfb97a7dd9596d6c63> . # participant
  }
}
WHERE {
    VALUES ?g {
      <http://mu.semte.ch/graphs/worship-service>
      <http://mu.semte.ch/graphs/administrative-unit>
      <http://mu.semte.ch/graphs/shared>
    }

    GRAPH ?g {
      ?memberOrganization org:memberOf ?organization .
    }

  BIND(SHA1(CONCAT(STR(?memberOrganization), STR(?organization), "member of")) as ?uuid)
  BIND(IRI(CONCAT("http://data.lblod.info/id/lidmaatschap/", ?uuid)) AS ?membership)
}
