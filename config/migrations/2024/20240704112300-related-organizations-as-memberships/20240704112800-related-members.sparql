PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH ?g {
    ?organization org:hasSubOrganization ?subOrganization .
  }
}
INSERT {
  GRAPH ?g {
    ?subOrganization org:memberOf ?organization .

    ?membership a org:Membership ;
      mu:uuid ?uuid ;
      org:organization ?organization ;
      org:member ?subOrganization ;
      org:role <http://data.lblod.info/id/rollen/4ec7d5c39bdc4e84b4174379b9e22ad8> . # gerelateerd
  }
}
WHERE {
    VALUES ?g {
      <http://mu.semte.ch/graphs/worship-service>
      <http://mu.semte.ch/graphs/administrative-unit>
      <http://mu.semte.ch/graphs/shared>
    }

    GRAPH ?g {
      ?organization org:hasSubOrganization ?subOrganization.
    }

  BIND(SHA1(CONCAT(STR(?organization), STR(?subOrganization), "sub organization")) as ?uuid)
  BIND(IRI(CONCAT("http://data.lblod.info/id/lidmaatschap/", ?uuid)) AS ?membership)
}