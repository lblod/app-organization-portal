PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
  GRAPH ?g {
    ?foundingOrg ext:isFounderOfOrganization ?foundedOrg .
  }
}
INSERT {
  GRAPH ?g {
    ?foundingOrg org:memberOf ?foundedOrg .

    ?membership a org:Membership ;
      mu:uuid ?uuid ;
      org:organization ?foundedOrg ;
      org:member ?foundingOrg ;
      org:role <http://data.lblod.info/id/rollen/73d5e1cf250d42fab15926771f07505a> . # stichtend lid
  }
}
WHERE {
    VALUES ?g {
      <http://mu.semte.ch/graphs/worship-service>
      <http://mu.semte.ch/graphs/administrative-unit>
      <http://mu.semte.ch/graphs/shared>
    }

    GRAPH ?g {
      ?foundingOrg ext:isFounderOfOrganization ?foundedOrg .
    }

  BIND(SHA1(CONCAT(STR(?foundingOrg), STR(?foundedOrg), "founded by")) as ?uuid)
  BIND(IRI(CONCAT("http://data.lblod.info/id/lidmaatschap/", ?uuid)) AS ?membership)
}